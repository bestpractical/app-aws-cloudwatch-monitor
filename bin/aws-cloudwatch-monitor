#!/usr/bin/env perl

# Copyright 2021 Best Practical Solutions, LLC <sales@bestpractical.com>
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

use strict;
use warnings;

use Getopt::Long qw(:config pass_through);
use Pod::Usage ();
use App::AWS::CloudWatch::Monitor;

my $VERSION = '0.06';

Getopt::Long::GetOptions(
    \my %opt,
    'check=s@',
    'from-cron',
    'verify',
    'retries=s',
    'initial-delay=s',
    'max-delay=s',
    'verbose',
    'version' => sub { print "aws-cloudwatch-monitor version $VERSION\n"; exit 0 },
    'help',
) or Pod::Usage::pod2usage( -exitval => 1 );

Pod::Usage::pod2usage( -exitval => 0 ) if ( $opt{help} );
Pod::Usage::pod2usage( -message => 'Option check is required', -exitval => 1 ) unless ( $opt{check} );

delete $opt{version};
delete $opt{help};

if ($opt{'from-cron'}) {
    $opt{verbose} = 0;
}

my $monitor = App::AWS::CloudWatch::Monitor->new();
$monitor->run(\%opt, \@ARGV);

exit 0;

__END__

=pod

=head1 NAME

aws-cloudwatch-monitor - collect and send metrics to AWS CloudWatch

=head1 SYNOPSIS

 aws-cloudwatch-monitor [--check <module>]
                        [--from-cron] [--verify] [--verbose]
                        [--retries <count>] [--initial-delay <seconds>] [--max-delay <seconds>]
                        [--version] [--help]

=head1 DESCRIPTION

C<aws-cloudwatch-monitor> is the commandline interface to L<App::AWS::CloudWatch::Monitor> for collecting and sending custom metrics to AWS CloudWatch from an AWS EC2 instance.

=head1 OPTIONS

=over

=item --check <module>

Defines the checks to run.

Multiple C<--check> options may be defined and are run in the order they're passed.

=item --retries <count>

Specifies the number of times to retry sending metrics to CloudWatch if the initial attempt fails.
It will retry if getting a 5xx response or a 400 with a message containing "Throttling".
If not specified, the default is 2 (so it will attempt to send metrics 3 times)

=item --initial-delay <seconds>

Specifies the initial delay in seconds before retrying to send metrics to CloudWatch.
If not specified, the default is 4.
Each subsequent retry will double the delay up to the maximum delay.

=item --max-delay <seconds>

Specifies the maximum delay in seconds before retrying to send metrics to CloudWatch.
If not specified, the default is 16.

Note: if you want to have a constant delay between retries, set --initial-delay and --max-delay
to the same value.

    aws-cloudwatch-monitor --retries 5 --initial-delay 30 --max-delay 30

=item --from-cron

Specifies that this script is running from cron.

C<--verbose> is forced to off and results information is suppressed if C<--from-cron> is set.

C<--from-cron> additionally adds a random sleep interval up to 20 seconds.

=item --verify

Checks configuration and prepares a remote call, but does not upload metrics to CloudWatch.

=item --verbose

Print the metrics payload and additional details while running.

=item --version

Print the version.

=item --help

Print the help menu.

=back

=head1 ADDITIONAL OPTIONS FOR CHECK MODULES

The check modules within this project may require additional options not directly defined in C<aws-cloudwatch-monitor>.

All additional options defined on the commandline are passed to the check modules.

For example, the L<App::AWS::CloudWatch::Monitor::Check::DiskSpace> check module requires the C<--disk-path> option, which is passed through and verified in the check module itself.

 aws-cloudwatch-monitor --check DiskSpace --disk-path /

If C<--disk-path> isn't defined, the C<DiskSpace> check module will warn and skip gathering its metrics.

 aws-cloudwatch-monitor --check DiskSpace
 error: Check::DiskSpace: Option: disk-path is required at lib/App/AWS/CloudWatch/Monitor/Check/DiskSpace.pm line 18.

Additional information about each check module can be found using the C<perldoc> program.  For example, documentation for the included C<DiskSpace> check module can be read by running the following command:

 perldoc App::AWS::CloudWatch::Monitor::Check::DiskSpace

=head1 CONFIGURATION

To send metrics to AWS, you need to provide the access key id and secret access key for your configured AWS CloudWatch service.  You can set these in the file C<config.ini>.

An example is provided as part of this distribution.  The user running the metric script, like the user configured in cron for example, will need access to the configuration file.

To set up the configuration file, copy C<config.ini.example> into one of the following locations:

=over

=item C<$ENV{HOME}/.config/aws-cloudwatch-monitor/config.ini>

=item C</etc/aws-cloudwatch-monitor/config.ini>

=back

After creating the file, edit and update the values accordingly.

B<NOTE:> If the C<$ENV{HOME}/.config/aws-cloudwatch-monitor/> directory exists, C<config.ini> will be loaded from there regardless of a config file in C</etc/aws-cloudwatch-monitor/>.

=cut
